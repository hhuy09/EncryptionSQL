/*---------------------------------------------------------- 
MASV: 18120397 - 18120399 - 18120423
HO TEN CAC THANH VIEN NHOM: 
	Nguyễn Đặng Hồng Huy
	Phạm Đức Huy
	Trịnh Tấn Khoa
LAB: 03 - NHOM 
NGAY: 28/04/2021
----------------------------------------------------------*/ 
USE [QLSVNhom]
GO

-- Thêm sinh viên
CREATE OR ALTER PROCEDURE SP_INS_SINHVIEN 
	@MASV	NVARCHAR(20),
	@HOTEN	NVARCHAR(100),
	@NGAYSINH	DATETIME,
	@DIACHI NVARCHAR(200),
	@MALOP	VARCHAR(20),
	@TENDN	NVARCHAR(100),
	@MATKHAU	VARCHAR(100)
AS
BEGIN
	SET NOCOUNT ON;
	DECLARE @MATKHAU_MD5 VARBINARY(100);
	SET @MATKHAU_MD5 = CONVERT(VARBINARY(100),HASHBYTES('MD5', @MATKHAU));
	INSERT INTO DBO.SINHVIEN
	VALUES (@MASV, @HOTEN, @NGAYSINH, @DIACHI, @MALOP, @TENDN, @MATKHAU_MD5)
END
GO

-- Login Nhân viên
CREATE OR ALTER PROCEDURE SP_LOGIN_NHANVIEN @USERNAME NVARCHAR(100), @PASSWORD VARCHAR(100)
AS
BEGIN
	DECLARE @MATKHAU VARBINARY(100);
	IF EXISTS (SELECT TENDN FROM DBO.NHANVIEN WHERE TENDN = @USERNAME) 
	BEGIN
		SET @MATKHAU = CONVERT(VARBINARY(MAX),HASHBYTES('SHA1', @PASSWORD));
		IF EXISTS (SELECT TENDN FROM DBO.NHANVIEN WHERE TENDN = @USERNAME AND MATKHAU = @MATKHAU)
		BEGIN
			PRINT N'Đăng nhập thành công'
		END
		ELSE
		BEGIN
			RAISERROR(N'Đăng nhập không thành công',16,1)
		END
	END
	ELSE
	BEGIN
		RAISERROR( N'Tài khoản không tồn tại',16,1)
	END
END
GO

--EXEC SP_LOGIN_NHANVIEN 'NVA', '123456'

-- Xem thông tin Nhân viên
CREATE OR ALTER PROCEDURE SP_SEL_NHANVIEN @TENDN NVARCHAR(100), @HOTEN NVARCHAR(100) OUTPUT, @MANV VARCHAR(20) OUTPUT
AS
BEGIN
	IF EXISTS (SELECT HOTEN FROM DBO.NHANVIEN WHERE TENDN = @TENDN)
		SELECT @HOTEN = HOTEN, @MANV = MANV FROM DBO.NHANVIEN WHERE TENDN = @TENDN
END
GO

-- Xem thông tin lớp học do nhân viên quan lý
CREATE OR ALTER PROCEDURE SP_SEL_LOP @MANV VARCHAR(20)
AS
BEGIN
	SELECT MALOP, TENLOP FROM DBO.LOP WHERE MANV = @MANV;
END
GO

--EXEC SP_SEL_LOP 'NV01'

-- Xem danh sach sinh vien trong lop
CREATE OR ALTER PROCEDURE SP_SEL_SINHVIEN_LOP  @MALOP VARCHAR(20)
AS
BEGIN
	SELECT MASV, HOTEN FROM DBO.SINHVIEN WHERE MALOP = @MALOP;
END
GO

--EXEC SP_SEL_SINHVIEN_LOP 'CNSH'

-- Xem thông tin sinh viên
CREATE OR ALTER PROCEDURE SP_SEL1_SINHVIEN 
	@MASV		NVARCHAR(20),
	@HOTEN		NVARCHAR(100)	OUTPUT, 
	@NGAYSINH	DATETIME		OUTPUT, 
	@DIACHI		NVARCHAR(200)	OUTPUT,
	@TENLOP		NVARCHAR(100)	OUTPUT
AS
BEGIN
	DECLARE @MALOP VARCHAR(20);
	SELECT @HOTEN = HOTEN, @NGAYSINH = NGAYSINH, @DIACHI = DIACHI, @MALOP = MALOP 
	FROM DBO.SINHVIEN 
	WHERE MASV = @MASV;
	SELECT @TENLOP = TENLOP
	FROM DBO.LOP 
	WHERE MALOP = @MALOP;
END
GO

-- Cập nhật thông tin sinh viên
CREATE OR ALTER PROCEDURE SP_UPDATE_SINHVIEN  
	@MASV		NVARCHAR(20),
	@HOTEN		NVARCHAR(100), 
	@NGAYSINH	DATETIME,
	@DIACHI		NVARCHAR(200),
	@TENLOP		NVARCHAR(100),
	@MANV		VARCHAR(20)
AS
BEGIN
	DECLARE @COUNT INT;
	SELECT @COUNT = COUNT(*)
	FROM SINHVIEN SV JOIN LOP L ON L.MALOP = SV.MALOP 
					 JOIN NHANVIEN NV ON NV.MANV = L.MANV
	WHERE SV.MASV = @MASV AND NV.MANV = @MANV

	DECLARE @MALOP VARCHAR(20);
	IF EXISTS (SELECT MALOP FROM DBO.LOP WHERE TENLOP = @TENLOP)
	BEGIN
		SET @MALOP = (SELECT MALOP FROM DBO.LOP WHERE TENLOP = @TENLOP);
		IF (@COUNT > 0)
		BEGIN
			UPDATE DBO.SINHVIEN
			SET HOTEN = @HOTEN, NGAYSINH = @NGAYSINH, DIACHI = @DIACHI, MALOP = @MALOP
			WHERE MASV = @MASV;
		END
		ELSE
		BEGIN
			RAISERROR(N'Không thể chỉnh sửa sinh viên này hoặc sinh viên này không tồn tại',16,1);
		END
	END
	ELSE
	BEGIN
		RAISERROR(N'Lớp không hợp lệ',16,1);
	END

END
GO

--EXEC SP_UPDATE_SINHVIEN N'18120397',N'Nguyễn Đặng Hồng Huy','03/01/2000',N'GL',N'Công Nghệ Thông Tin',N'NV01'

-- Xem bảng điểm sinh viên
CREATE OR ALTER PROCEDURE SP_SEL_BANGDIEM 
	@MASV	NVARCHAR(20),
	@MANV	NVARCHAR(20),
	@MK		VARCHAR(100)
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.SINHVIEN WHERE MASV = @MASV)
	BEGIN
		SELECT H.MAHP, H.TENHP, H.SOTC, CONVERT(VARCHAR(MAX), DECRYPTBYASYMKEY(ASYMKEY_ID(@MANV),B.DIEMTHI, CONVERT(NVARCHAR(100),@MK))) "DIEMTHI"
		FROM DBO.HOCPHAN H, DBO.BANGDIEM B
		WHERE H.MAHP = B.MAHP AND B.MASV = @MASV
	END
	ELSE
	BEGIN
		RAISERROR(N'Sinh viên không tồn tại',16,1);
	END
END
GO

--EXEC SP_SEL_BANGDIEM '18120397', N'NV01', '123456'

-- Cập nhật bảng điểm
CREATE OR ALTER PROCEDURE SP_UPDATE_BANGDIEM 
	@MASV		NVARCHAR(20),
	@MAHP		VARCHAR(20),
	@DIEMTHI	VARCHAR(50),
	@MANV		NVARCHAR(20),
	@MK			VARCHAR(100)
AS
BEGIN
	DECLARE @SQL NVARCHAR(MAX);
	IF EXISTS (SELECT * FROM DBO.BANGDIEM WHERE MAHP = @MAHP AND MASV = @MASV)
	BEGIN
		IF ASYMKEY_ID(@MANV) IS NULL
		BEGIN
			SET @SQL = 'CREATE ASYMMETRIC KEY '  + QUOTENAME(@MANV) + ' ' +
					 'WITH ALGORITHM = RSA_512 ' + 
					 'ENCRYPTION BY PASSWORD = ' + QUOTENAME(@MK , NCHAR(39))
			EXEC (@SQL)
		END
		UPDATE DBO.BANGDIEM
		SET DIEMTHI = ENCRYPTBYASYMKEY(ASYMKEY_ID(@MANV), @DIEMTHI)
		WHERE MASV = @MASV AND MAHP = @MAHP;
	END
	ELSE
	BEGIN
		RAISERROR(N'Không tồn tại',16,1);
	END
END
GO

--EXEC SP_UPDATE_BANGDIEM N'18120397', 'MATH01', '9.0', N'NV01', '123456'

-- Xem danh sách sinh viên do nhân viên đó quản lý
CREATE OR ALTER PROCEDURE SP_SEL_SINHVIEN_NHANVIEN @MANV NVARCHAR(20)
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.NHANVIEN WHERE MANV = @MANV)
	BEGIN
		SELECT SV.MASV, SV.HOTEN, SV.MALOP, L.TENLOP, SV.NGAYSINH, SV.TENDN, SV.MATKHAU
		FROM DBO.SINHVIEN SV, DBO.LOP L
		WHERE SV.MALOP = L.MALOP AND L.MANV = @MANV
	END
	ELSE
	BEGIN
		RAISERROR(N'Không tồn tại',16,1);
	END
END
GO

--EXEC SP_SEL_SINHVIEN_NHANVIEN N'NV01'

-- Tìm mã lớp
CREATE OR ALTER PROCEDURE SP_MALOP_LOP @TENLOP NVARCHAR(100), @MALOP VARCHAR(20) OUT
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.LOP WHERE TENLOP = @TENLOP)
	BEGIN
		SET @MALOP = (SELECT MALOP FROM DBO.LOP WHERE TENLOP = @TENLOP);
	END
	ELSE		
	BEGIN
		RAISERROR(N'Không tồn tại lớp này',16,1);
	END
END
GO

--EXEC SP_MALOP_LOP N'Công nghệ thông tin', ''

-- Kiem tra lop co do nhan vien quan ly khong
CREATE OR ALTER PROCEDURE SP_CHECK_LOP_NV @MALOP VARCHAR(20), @MANV NVARCHAR(20)
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.LOP WHERE MANV = @MANV AND MALOP = @MALOP)
	BEGIN
		PRINT N'Có quản lý lớp này.';
	END
	ELSE
	BEGIN
		RAISERROR(N'Nhân viên không quản lý lớp này.',16,1);
	END
END
GO

-- EXEC SP_CHECK_LOP_NV 'CNTT', 'NV02'

-- Xóa sinh viên
CREATE OR ALTER PROCEDURE SP_DEL_SINHVIEN @MASV NVARCHAR(20)
AS
BEGIN
	DELETE DBO.BANGDIEM
	WHERE MASV = @MASV;
	DELETE DBO.SINHVIEN
	WHERE MASV = @MASV;
END
GO

-- EXEC SP_DEL_SINHVIEN '1712308'

-- Thêm lớp
CREATE OR ALTER PROCEDURE SP_INS_LOP 
	@MALOP	VARCHAR(20), 
	@TENLOP NVARCHAR(100),
	@MANV	NVARCHAR(20)
AS
BEGIN
	INSERT INTO DBO.LOP
	VALUES (@MALOP, @TENLOP, @MANV);
END
GO

-- EXEC SP_INS_LOP 'VLKT', N'Vật lý kỹ thuật', 'NV01'

-- Xóa lớp
CREATE OR ALTER PROCEDURE SP_DEL_LOP @MALOP	VARCHAR(20)
AS
BEGIN 
	IF EXISTS (SELECT * FROM DBO.SINHVIEN WHERE MALOP = @MALOP)
	BEGIN
		RAISERROR(N'Lớp có sinh viên, không xóa được lớp này.',16,1);
	END
	ELSE
	BEGIN
		DELETE DBO.LOP
		WHERE MALOP = @MALOP;
	END 
END
GO

-- EXEC SP_DEL_LOP 'CNSH'

-- Xem thông tin lớp học
CREATE OR ALTER PROCEDURE SP_SEL1_LOP @MALOP VARCHAR(20), @TENLOP NVARCHAR(100) OUT
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.LOP WHERE MALOP = @MALOP)
	BEGIN
		SET @TENLOP = (SELECT TENLOP FROM DBO.LOP WHERE MALOP = @MALOP);
	END
END
GO

--EXEC SP_SEL1_LOP 'CNTT', ''

-- Cập nhật thông tin lớp học
CREATE OR ALTER PROCEDURE SP_UPD_LOP @MALOP VARCHAR(20), @TENLOP NVARCHAR(100)
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.LOP WHERE MALOP = @MALOP)
	BEGIN
		UPDATE DBO.LOP
		SET TENLOP = @TENLOP
		WHERE MALOP = @MALOP;
	END
END
GO

-- EXEC SP_UPD_LOP 'HTTT', 'HTTT'

-- Xem danh sach DKHP
CREATE OR ALTER PROCEDURE SP_SEL_DKHP 
	@MANV	NVARCHAR(20),
	@MK		VARCHAR(100)
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.LOP WHERE MANV = @MANV)
	BEGIN
		SELECT B.MASV, SV.HOTEN, H.MAHP, H.TENHP, H.SOTC, CONVERT(VARCHAR(MAX), DECRYPTBYASYMKEY(ASYMKEY_ID(@MANV),B.DIEMTHI, CONVERT(NVARCHAR(100),@MK))) "DIEMTHI"
		FROM DBO.HOCPHAN H, DBO.BANGDIEM B, DBO.SINHVIEN SV, DBO.LOP L
		WHERE H.MAHP = B.MAHP AND B.MASV = SV.MASV AND L.MALOP = SV.MALOP AND L.MANV = @MANV
	END
	ELSE
	BEGIN
		RAISERROR(N'Không hợp lệ.',16,1);
	END
END
GO

-- EXEC SP_SEL_DKHP  N'NV02', '1234567'

-- Thêm dang ky hoc phan
CREATE OR ALTER PROCEDURE SP_INS_DKHP @MASV NVARCHAR(20), @MAHP VARCHAR(20)
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.SINHVIEN WHERE MASV = @MASV)
	BEGIN
		IF EXISTS (SELECT * FROM DBO.HOCPHAN WHERE @MAHP = MAHP)
		BEGIN
			INSERT INTO DBO.BANGDIEM (MASV, MAHP)
			VALUES (@MASV, @MAHP)
		END
		ELSE
		BEGIN
			RAISERROR(N'Môn học không tồn tại.',16,1);
		END
	END
	ELSE
	BEGIN
		RAISERROR(N'Sinh viên không tồn tại.',16,1);
	END
END
GO

-- EXEC SP_INS_DKHP '1612647', 'DCVN01'

-- Xem ma hoc phần
CREATE OR ALTER PROCEDURE SP_SEL1_MAHP @TENHP NVARCHAR(100), @MAHP VARCHAR(20) OUT
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.HOCPHAN WHERE TENHP = @TENHP)
	BEGIN
		SET @MAHP = (SELECT MAHP FROM DBO.HOCPHAN WHERE TENHP = @TENHP)
	END
	ELSE
	BEGIN
		RAISERROR(N'Học phần không tồn tại.',16,1);
	END
END
GO

-- EXEC SP_SEL1_MAHP N'Mác Lê Nin', ''

-- Xoa DKHP
CREATE OR ALTER PROCEDURE SP_DEL_DKHP @MASV NVARCHAR(20), @MAHP VARCHAR(20)
AS
BEGIN
	IF EXISTS (SELECT * FROM DBO.BANGDIEM WHERE MASV = @MASV AND MAHP = @MAHP)
	BEGIN
		DELETE DBO.BANGDIEM
		WHERE MASV = @MASV AND MAHP = @MAHP;
	END
	ELSE
	BEGIN
		RAISERROR(N'DKHP không tồn tại.',16,1);
	END
END
GO

-- EXEC SP_DEL_DKHP '1612647', 'DCVN01'

-- Xem thong tin DKHP 1 sv
CREATE OR ALTER PROCEDURE SP_SEL1_DKHP 
	@MASV	NVARCHAR(20),
	@MAHP	NVARCHAR(20),
	@HOTEN	NVARCHAR(100)	OUT,
	@TENHP	NVARCHAR(100)	OUT
AS
BEGIN
	SET @HOTEN = (SELECT HOTEN
	FROM DBO.SINHVIEN 
	WHERE MASV = @MASV);
	SET @TENHP = (SELECT TENHP
	FROM DBO.HOCPHAN 
	WHERE MAHP = @MAHP);
END
GO
